buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

plugins {
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'com.github.kt3k.coveralls' version '2.0.1'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.1'
}


project.description = 'LedBorg Library'
project.archivesBaseName = 'de.freitag.stefan.ledborg'
project.version = '1.4-RELEASE'

mainClassName = 'de.freitag.stefan.ledborg.demo.LedBorgDemo'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'com.pi4j', name: 'pi4j-core', version: '0.0.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

jar {
    exclude('log4j2.xml')
    manifest {
        attributes 'Implementation-Title': 'LedBorg Library',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Stefan Freitag',
                'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}


def pomConfig = {
    inceptionYear '2015'

    licenses {
        license {
            name "The MIT License (MIT)"
            url "https://github.com/stefanfreitag/LedBorg/blob/master/LICENSE"
            distribution "repo"
        }
    }

    scm {
        connection 'scm:git:https://github.com/stefanfreitag/LedBorg/'
        developerConnection 'scm:git:https://github.com/stefanfreitag/LedBorg/'
        url 'https://github.com/stefanfreitag/LedBorg/'
    }
    developers {
        developer {
            id "sfreitag"
            name "Stefan Freitag"
            email "stefan@stefreitag.de"
            timezone "Europe/Berlin"
            roles {
                role "developer"
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar
            artifact distZip

            groupId 'de.freitag.stefan'
            artifactId 'ledborg'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java Library for accessing the LedBorg hardware')
                root.children().last() + pomConfig
            }
        }
    }
}

bintrayUpload.dependsOn(build)

bintray {
    if (!project.hasProperty('bintray_user')) {
        project.ext.bintray_user = 'default'
    }
    if (!project.hasProperty('bintray_api_key')) {
        project.ext.bintray_api_key = 'default'
    }

    user = "$bintray_user"
    key = "$bintray_api_key"
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'LedBorg'
        desc = 'Java Library for accessing the LedBorg hardware.'
        websiteUrl = 'https://github.com/stefanfreitag/LedBorg/'
        issueTrackerUrl = 'https://github.com/stefanfreitag/LedBorg/issues'
        vcsUrl = 'git://github.com/stefanfreitag/LedBorg.git'
        licenses = ['MIT']
        labels = []
        publicDownloadNumbers = true
	version {
        name = project.version
        vcsTag = project.version
        gpg {
            sign = true
            passphrase = project.hasProperty('gpg_passphrase') ? gpg_passphrase : ''
        }
        }
    }
}

applicationDistribution.from("src/main/resources/log4j2.xml") {
    into "bin"
}

startScripts {
    applicationName = project.archivesBaseName
    classpath += files('bin')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\bin', '%APP_HOME%\\bin')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/bin', '$APP_HOME/bin')
    }
}