buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.7.1'
    }
}

plugins {
    id 'application'
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
}

project.description = 'LedBorg Demo'
project.version = '1.0.1'

mainClassName = 'de.freitag.stefan.ledborg.demo.LedBorgDemo'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
}

dependencies {
    compile project(':borg-core')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

jar {
    manifest {
        attributes  'Implementation-Title': 'LedBorg Demo',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Stefan Freitag',
                'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

findbugs {
    ignoreFailures = true
    effort = "max"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = true
    ruleSets = [
            "java-basic",
            "java-braces",
            "java-design",
            "java-finalizers",
            "java-junit",
            "java-naming",
            "java-optimizations",
            "java-strings",
            "java-unusedcode"
    ]
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

def pomConfig = {
    inceptionYear '2015'

    licenses {
        license {
            name "The MIT License (MIT)"
            url "https://github.com/stefanfreitag/LedBorg/blob/master/LICENSE"
            distribution "repo"
        }
    }

    scm {
        connection 'scm:git:https://github.com/stefanfreitag/LedBorg/'
        developerConnection 'scm:git:https://github.com/stefanfreitag/LedBorg/'
        url 'https://github.com/stefanfreitag/LedBorg/'
    }
    developers {
        developer {
            id "sfreitag"
            name "Stefan Freitag"
            email "stefan@stefreitag.de"
            timezone "Europe/Berlin"
            roles {
                role "developer"
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar
            artifact distZip

            groupId 'de.freitag.stefan.ledborg'
            artifactId 'borg-demo'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'LedBorg Demo Application')
                root.children().last() + pomConfig
            }
        }
    }
}

bintrayUpload.dependsOn(build)

bintray {
    if (!project.hasProperty('bintray_user')) {
        project.ext.bintray_user = 'default'
    }
    if (!project.hasProperty('bintray_api_key')) {
        project.ext.bintray_api_key = 'default'
    }

    user = "$bintray_user"
    key = "$bintray_api_key"
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'borg-demo'
        desc = 'LedBorg Demo Application'
        websiteUrl = 'https://github.com/stefanfreitag/LedBorg/'
        issueTrackerUrl = 'https://github.com/stefanfreitag/LedBorg/issues'
        vcsUrl = 'git://github.com/stefanfreitag/LedBorg.git'
        licenses = ['MIT']
        labels = []
        publicDownloadNumbers = true
	version {
        name = project.version
        vcsTag = project.version
        gpg {
            sign = true
            passphrase = project.hasProperty('gpg_passphrase') ? gpg_passphrase : ''
        }
        }
    }
}
